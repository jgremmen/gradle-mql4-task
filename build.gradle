/*
buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath files("${buildDir}/libs/gradle-mql4-task-1.0.1.jar")
  }
}
*/


plugins {
  id 'maven-publish'
  id 'java-gradle-plugin'
  id 'signing'
//  id 'de.sayayi.gradle.mql4-plugin'
}


description = "Gradle MQL4 Compile Task"
group = 'de.sayayi.gradle'
version = rootProject.file('version.txt').text.trim()

sourceCompatibility = 1.8
targetCompatibility = 1.8


repositories {
  jcenter()
}


dependencies {
  annotationProcessor "org.projectlombok:lombok:1.18.6"
  compileOnly "org.projectlombok:lombok:1.18.6"

  testImplementation gradleTestKit()
  testImplementation 'junit:junit:4.12'
}


gradlePlugin {
  plugins {
    mql4Plugin {
      id = 'de.sayayi.gradle.mql4-plugin'
      implementationClass = 'de.sayayi.gradle.mql4.task.CompileMQL4TaskPlugin'
    }
  }
}


jar {
  manifest {
    attributes["Implementation-Title"] = rootProject.description
    attributes["Implementation-Version"] = rootProject.version
    attributes["Implementation-Vendor"] = "Jeroen Gremmen"
  }

  from("${projectDir}") {
    include "LICENSE"
    into "META-INF"
  }
}


task sourcesJar(type: Jar) {
  from sourceSets.main.allJava
  classifier = 'sources'
}


task javadocJar(type: Jar) {
  from javadoc
  classifier = 'javadoc'
}


publishing {
  repositories {
    maven {
/*
      url = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
      credentials {
        username = 'xxx'
        password = 'yyy'
      }
*/
      url = "$buildDir/repo"
    }
  }

  publications {
    maven(MavenPublication) {
      from components.java
      artifact sourcesJar
      artifact javadocJar

      pom {
        name = project.description
        description = 'Gradle Task for compiling MetaTrader MQL4 files'
        url = 'https://github.com/jgremmen/gradle-mql4-task'
        inceptionYear = '2019'

        licenses {
          license {
            name = 'Apache License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0'
            distribution = 'repo'
          }
        }

        developers {
          developer {
            id = 'jgremmen'
            name = 'Jeroen Gremmen'
            email = 'jeroen.gremmen@sayayi.de'
          }
        }

        scm {
          connection = 'scm:git:git://github.com/jgremmen/gradle-mql4-task.git'
          developerConnection = 'scm:git:git://github.com/jgremmen/gradle-mql4-task.git'
          url = 'https://github.com/jgremmen/gradle-mql4-task'
        }
      }
    }
  }
}


signing {
  sign publishing.publications.maven
}


/*
mql4 {
  mql4Dir file("D:/project/jfx-mt4/jfx-tm/mt4/template/MQL4")
  metaeditor "D:/project/jfx-mt4/jfx-tm/mt4/template/metaeditor.exe"
  verbose = true
}
*/
