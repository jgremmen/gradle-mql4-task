plugins {
  id "com.gradle.plugin-publish" version "0.10.1"
  id 'maven-publish'
  id 'java-gradle-plugin'
  id 'signing'
  id "de.undercouch.download" version "3.4.3"
}


description = "Gradle MQL4 Compile Task"
group = 'de.sayayi.gradle'
version = rootProject.file('version.txt').text.trim()

sourceCompatibility = 1.8
targetCompatibility = 1.8


repositories {
  jcenter()
}


dependencies {
  annotationProcessor "org.projectlombok:lombok:1.18.6"

  compileOnly gradleApi()
  compileOnly "org.projectlombok:lombok:1.18.6"

  testImplementation gradleTestKit()
  testImplementation 'org.junit.jupiter:junit-jupiter:5.4.0'
  testImplementation 'io.github.glytching:junit-extensions:2.3.0'
}


gradlePlugin {
  plugins {
    mql4Plugin {
      id = 'de.sayayi.gradle.mql4-plugin'
      implementationClass = 'de.sayayi.gradle.mql4.task.CompileMQL4TaskPlugin'
    }
  }
}


pluginBundle {
  website = 'https://github.com/jgremmen/gradle-mql4-task'
  vcsUrl = 'https://github.com/jgremmen/gradle-mql4-task'
  description = project.description
  tags = [ 'mql4', 'mq4', 'metaeditor' ]

  plugins {
    mql4Plugin {
      displayName = 'Gradle MQL4 Plugin - compile MQ4 indicator, expert advisor and script files.'
    }
  }
}


jar {
  manifest {
    attributes["Implementation-Title"] = rootProject.description
    attributes["Implementation-Version"] = rootProject.version
    attributes["Implementation-Vendor"] = "Jeroen Gremmen"
  }

  from("${projectDir}") {
    include "LICENSE"
    into "META-INF"
  }
}


task packageSources(type: Jar) {
  from sourceSets.main.allJava
  classifier = 'sources'
}


task packageJavadoc(type: Jar) {
  from javadoc
  classifier = 'javadoc'
}


publishing {
  repositories {
    maven {
/*
      url = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
      credentials {
        username = 'xxx'
        password = 'yyy'
      }
*/
      url = "$buildDir/repo"
    }
  }

  publications {
    maven(MavenPublication) {
      from components.java
      artifact packageSources
      artifact packageJavadoc

      pom {
        name = project.description
        description = 'Gradle Task for compiling MetaTrader MQ4 files'
        url = 'https://github.com/jgremmen/gradle-mql4-task'
        inceptionYear = '2019'

        licenses {
          license {
            name = 'Apache License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0'
            distribution = 'repo'
          }
        }

        developers {
          developer {
            id = 'jgremmen'
            name = 'Jeroen Gremmen'
            email = 'jeroen.gremmen@sayayi.de'
          }
        }

        scm {
          connection = 'scm:git:git://github.com/jgremmen/gradle-mql4-task.git'
          developerConnection = 'scm:git:git://github.com/jgremmen/gradle-mql4-task.git'
          url = 'https://github.com/jgremmen/gradle-mql4-task'
        }
      }
    }
  }
}


signing {
  sign publishing.publications.maven
}


test {
  doFirst {
    download {
      src 'https://github.com/EA31337/MetaEditor/raw/master/metaeditor.exe'
      dest buildDir
      overwrite false
    }

    systemProperty "MQL4_BASE", sourceSets.test.resources.srcDirs[0]
    systemProperty "METAEDITOR", "${buildDir}/metaeditor.exe"
  }

  useJUnitPlatform()

  testLogging {
    events "passed", "skipped", "failed"
  }
}


/*
mql4 {
  mql4Dir file("D:/project/jfx-mt4/jfx-tm/mt4/template/MQL4")
  metaeditor "D:/project/jfx-mt4/jfx-tm/mt4/template/metaeditor.exe"
  verbose = true

  wine {
    executable = "wine64"
    enabled = false
  }
}
*/
